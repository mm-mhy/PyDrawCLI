//通过解析器代码将元模型实例化，调用对象属性来解析指令
//元模型场景对象
Scene:
    background_color=Background_Color?
    axis=Axis?
    shapedefine*=ShapeDefine
    draw_instructions*=DrawInstruction
    transform*=Transform
    drawlines*=Drawlines
    drawcurves*=Drawcurves
    print*=PrintF
;

Drawcurves:
    'drawcurve' points+=CurvePoints
;

CurvePoints:
    x=INT ',' y=INT
;

Drawlines:
    'drawline' points+=LinePoints
;

LinePoints:
    x=INT ',' y=INT
;

ShapeDefine:
    shapes*=Shape
    square*=Square
    circle*=Circle
;
//背景颜色指令格式
Background_Color:
    'bgcolor' color=Color
;

//坐标轴指令格式
Axis:
    'axis' isaxis=BOOL
;

//图形变换指令格式
Transform:
    translate=Translation|rotate=Rotation|scale=Scale
;
//图形平移指令格式
Translation:
    'translate' shape=[Graph_select] x=INT ',' y=INT
;
//图形旋转指令格式
Rotation:
    'rotate' shape=[Graph_select] angle=FLOAT
;
//图形缩放指令格式
Scale:
    'scale' shape=[Graph_select] x=FLOAT ',' y=FLOAT
;

//自定义几何图形指令格式
Shape:
    'shape' name=ID extends=Extends?
        line_color=LineColor?
        line_size=LineSize?
        fill_color=FillColor?
        lines+=Line
        curves*=Curve
    'end'
;

//定义圆指令格式
Circle:
    'circle' name=ID extends=Extends?
        line_color=LineColor?
        line_size=LineSize?
        fill_color=FillColor?
        radius=Radius
    'end'
;
//定义正方形指令格式
Square:
    'square' name=ID extends=Extends?
        line_color=LineColor?
        line_size=LineSize?
        fill_color=FillColor?
        special=Special
    'end'
;

//继承
Extends:
    'extends' shape=[Shape]
;

//线条大小指令格式
LineSize:
    'linesize' size=INT
;

//正方形特殊指令格式
Special:
    Size | TwoPoint
;
//正方形长宽法绘制
Size:
    'width' width=INT  'height' height=INT
;
//正方形对角线法绘制
TwoPoint:
    'lrc' x1=INT ',' y1=INT
;

//半径
Radius:
    'radius' r+=R
;
R:
    INT | FLOAT
;

//打印到屏幕
PrintF:
    'print' text=STRING 'at' x=INT ',' y=INT
;

//直线颜色列表
Color:
    'red' | 'green' | 'blue' | 'yellow' | 'magenta' | 'cyan' | 'black' | 'white' | /#[0-9a-f]{6}/
;
//直线颜色关键字
LineColor: 
    'lines' color=Color
;
//填充颜色关键字
FillColor:
    'fill' color=Color
;

//绘制直线关键字
Line:
    'line' direction=Direction length=INT
;
//曲线
Curve:
    'curve' ctrlpoint+=CtrlPoint
;
//控制点
CtrlPoint:
    x=INT ',' y=INT
;
//方向
Direction:
    bearing=Bearing | angle=Angle
;
//可选择列表
Bearing:
    'E' | 'NE' | 'SE' | 'W' | 'NW' | 'SW' | 'N' | 'S'
;
//度数
Angle:
    degrees=FLOAT ('°' | 'deg')
;

//绘图基本指令
DrawInstruction:
    'draw' shape=[Graph_select] /*ID引用*/
    text=InnerText?/*添加文字*/
    'at' x=INT ',' y=INT  
    inwhere=IN?/*定位*/

;
//图形选择
Graph_select:
    Shape | Circle | Square
;
//图形嵌入指令格式
IN:
    'in' shape=[Shape]//引用Shape对象
;
//图形文本指令格式
InnerText:
    '(' text=STRING ')'
;

//注释
Comment:
    /\/\*(.|\n)*?\*\// | /\/\/.*?$/
;

//   ?  :该属性是可选的
//   *  :该属性可以出现0次或多次
//   +  :该属性可以出现1次或多次
//   [] :该属性可以出现0次或1次，引用对象
//   |  :表示选择