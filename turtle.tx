Scene:
    background_color=Background_Color?
    shapes*=Shape
    print*=PrintF
    square*=Square
    circle*=Circle
    draw_instructions*=DrawInstruction
    transform*=Transform
;
Background_Color:
    'bgcolor' color=Color
;

Transform:
    translate=Translation|rotate=Rotation|scale=Scale
;
Translation:
    'translate' shape=[Graph_select] x=INT ',' y=INT
;
Rotation:
    'rotate' shape=[Graph_select] angle=FLOAT
;
Scale:
    'scale' shape=[Graph_select] x=FLOAT ',' y=FLOAT
;


Shape:
    'shape' name=ID
        line_color=LineColor?
        line_size=LineSize?
        fill_color=FillColor?
        lines+=Line
        curves*=Curve
    'end'
;
Circle:
    'circle' name=ID
        line_color=LineColor?
        fill_color=FillColor?
        radius=Radius
    'end'
;
Square:
    'square' name=ID
        line_color=LineColor?
        fill_color=FillColor?
        special=Special
    'end'
;
LineSize:
    'linesize' size=INT
;
Special:
    Size | TwoPoint
;
Size:
    'width' width=INT  'height' height=INT
;
TwoPoint:
    'lrc' x1=INT ',' y1=INT
;
//半径
Radius:
    'radius' r+=R
;
R:
    INT | FLOAT
;

//打印到屏幕
PrintF:
    'print' text=STRING 'at' x=INT ',' y=INT
;
//直线颜色列表
Color:
    'red' | 'green' | 'blue' | 'yellow' | 'magenta' | 'cyan' | 'black' | 'white' | /#[0-9a-f]{6}/
;
//直线颜色关键字
LineColor: 
    'lines' color=Color
;
//填充颜色关键字
FillColor:
    'fill' color=Color
;
//绘制直线关键字
Line:
    'line' direction=Direction length=INT
;
//曲线
Curve:
    'curve' ctrlpoint+=CtrlPoint
;
//控制点
CtrlPoint:
    x=INT ',' y=INT
;

Direction:
    bearing=Bearing | angle=Angle
;
//可选择列表
Bearing:
    'E' | 'NE' | 'SE' | 'W' | 'NW' | 'SW' | 'N' | 'S'
;
//度数
Angle:
    degrees=FLOAT ('°' | 'deg')
;
//绘图基本指令
DrawInstruction:
    'draw' shape=[Graph_select] /*ID引用*/
    text=InnerText?/*添加文字*/
    'at' x=INT ',' y=INT  
    inwhere=IN?/*定位*/

;

Graph_select:
    Shape | Circle | Square
;

IN:
    'in' shape=[Shape]//引用Shape对象
;
InnerText:
    '(' text=STRING ')'
;


Comment:
    /\/\*(.|\n)*?\*\// | /\/\/.*?$/
;



//   ?  :该属性是可选的
//   *  :该属性可以出现0次或多次
//   +  :该属性可以出现1次或多次
//   [] :该属性可以出现0次或1次，引用对象


//   |  :表示选择
